plugins {
    id 'java'
    id 'me.qoomon.git-versioning' version '6.4.2'
    id 'org.springframework.boot' version '3.0.3'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'net.mrchar'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.javamoney:moneta:1.4.2'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

gitVersioning.apply {
    refs {
        considerTagsOnBranches = true
        branch('release/v(?<version>.+)') {
            version = '${ref.version}-SNAPSHOT+${commit.short}'
        }
        tag('v(?<version>[.\\d]+)\\-alpha') {
            version = '${ref.version}-alpha+${commit.short}'
        }
        tag('v(?<version>[.\\d]+)\\-beta') {
            version = '${ref.version}-beta+${commit.short}'
        }
        tag('v(?<version>[.\\d]+)') {
            version = '${ref.version}+${commit.short}'
        }
    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = 'unversioned+${commit.short}'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
